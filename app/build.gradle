buildscript {
    repositories {
        mavenCentral()
    }
}

plugins {
    id 'com.android.application'
    id 'kotlin-android'
    // Provides auto-uploading of proguard mappings to Sentry
    id 'io.sentry.android.gradle' version '4.5.1'
}

/**
 * Sentry has it's own Timber integration, which is automatically added with the
 * "io.sentry.android.gradle" plugin; we do NOT want to enable this as it will result in
 * errors being reported twice (with different messages)
 * https://docs.sentry.io/platforms/android/configuration/integrations/timber/
 */
configurations.configureEach {
    exclude group: "io.sentry", module: "sentry-android-timber"
}

android {
    compileSdkVersion versions.compileSdk

    kotlin {
        jvmToolchain(17)
    }

    buildFeatures {
        viewBinding true
    }

    defaultConfig {
        applicationId "com.steamclock.steamclogsample"
        minSdkVersion 21
        targetSdkVersion versions.compileSdk
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        debug {
            debuggable true
            minifyEnabled true // Always minify so that we can test proguard
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    namespace 'com.steamclock.steamclogsample'
}

dependencies {
    implementation project(':steamclog')
    // Since Sentry is a dependency of steamclog, we do not have to import it again
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-android:1.9.0'
    implementation 'androidx.appcompat:appcompat:1.7.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.2.0'
    implementation 'com.google.android.material:material:1.12.0'
}