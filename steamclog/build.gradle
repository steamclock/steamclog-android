plugins {
    id 'com.android.library'
    id 'kotlin-android'
    id 'maven-publish'
    // Note, do not apply sentry plugins here; must be done in application module
}

// Because the components are created only during the afterEvaluate phase, you must
// configure your publications using the afterEvaluate() lifecycle method.
afterEvaluate {
    publishing {
        publications {
            // Creates a Maven publication called "release".
            release(MavenPublication) {
                // Applies the component for the release build variant.
                from components.release

                // You can then customize attributes of the publication as shown below.
                groupId = 'com.steamclock.steamclog'
                artifactId = 'release'
                version = 'v2.3'
            }
        }
    }
}

android {
    compileSdkVersion versions.compileSdk

    // ADD COMPATIBILITY OPTIONS TO BE COMPATIBLE WITH JAVA 1.8
    // Added for Sentry support
    compileOptions {
        sourceCompatibility = JavaVersion.VERSION_1_8
        targetCompatibility = JavaVersion.VERSION_1_8
    }

    defaultConfig {
        minSdkVersion 21
        targetSdkVersion versions.compileSdk
        versionCode 3
        versionName "2.2"
        consumerProguardFiles "consumer-rules.pro"
    }

    buildTypes {
        debug {
            debuggable true
            minifyEnabled false
        }
        release {
            debuggable false
            minifyEnabled false
        }
    }
    namespace 'com.steamclock.steamclog'
}

dependencies {
    implementation fileTree(dir: "libs", include: ["*.jar"])
    // https://blog.jetbrains.com/kotlin/2020/07/kotlin-1-4-rc-released/
    // No longer need to include kotlin stdlib dependency
    implementation "org.jetbrains.kotlin:kotlin-reflect:${versions.kotlin}"

    implementation "com.jakewharton.timber:timber:${versions.timber}"
    // https://github.com/getsentry/sentry-java/releases
    implementation "io.sentry:sentry-android:${versions.sentry}"
}